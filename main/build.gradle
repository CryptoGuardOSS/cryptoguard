apply plugin: 'application'
mainClassName = 'main.rule.engine.RuleEngine'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()
    }
}

configurations {
    xsd2java
}

dependencies {
    xsd2java "com.sun.xml.bind:jaxb-xjc:2.2.6"
    xsd2java "com.sun.xml.bind:jaxb-impl:2.2.6"
}

//Only Run this command to re-generate the schema files
task xsd2java() {
    ext {
        scarfPackage = 'com.example.response'
        scarfSchema = 'src/main/schema/xsd/Scarf/Scarf.xsd'
    }

    System.setProperty('javax.xml.accessExternalSchema', 'all')

    doLast {
        file("src/main/java").mkdirs()

        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xsd2java.asPath)
        ant.jaxbTargetDir = file("src/main/java")

        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: scarfPackage,
                schema: scarfSchema
        ) {
            //arg(value: "-Xannotate")
        }

        //Heavily Hard Coded, removes the namespace so the xml output doesn't have a namespace : nmls
        //ant.replace(file:"src/main/java/com/example/response/package-info.java",token:"namespace = \"https://www.swamp.com/com/scarf/struct\", ",value:"")

    }
}

jar {

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes(
                'Main-Class': 'main.frontEnd.Interface.EntryPoint'
        )
    }
}

sourceSets {
    test {
        java {
            srcDirs = ["src/main/test"]
        }
    }
}

javadoc {
    exclude "com/example/response/**"
}

dependencies {
    compile name: 'soot-trunk'
    compile "org.codehaus.groovy:groovy:2.5.2"
    compile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
    compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile 'commons-cli:commons-cli:1.4'
}

//compileJava.dependsOn xsd2java